# Maintainer: Michael Spencer <sonrisesoftware@gmail.com>

pkgname=libpapyros
pkgver=0.1.0
pkgrel=1
pkgdesc="Library of common classes for Papyros"
arch=("i686" "x86_64")
url="https://github.com/papyros/libpapyros"
license=("GPL")
depends=("qt5-base-git" "qt5-declarative" "kconfig")
makedepends=("git" "python" "extra-cmake-modules")
source=("$pkgname::git+https://github.com/papyros/libpapyros.git#tag=v${pkgver}")
sha256sums=("SKIP")

prepare() {
	mkdir -p build
}

build() {
	cd build
	cmake "$srcdir/$pkgname" \
			-DCMAKE_INSTALL_PREFIX=/usr \
			-DLIB_INSTALL_DIR=lib \
			-DLIBEXEC_INSTALL_DIR=lib \
			-DQML_INSTALL_DIR=lib/qt/qml \
			-DKDE_INSTALL_USE_QT_SYS_PATHS=ON
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}

# Additional functions to generate a changelog

changelog() {
    cd "$pkgname"
  	git log $1..HEAD --no-merges --format=" * %s"
}

gitref() {
    cd "$pkgname"
  	git rev-parse HEAD
}
