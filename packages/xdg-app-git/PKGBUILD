# Maintainer: Mirco Tischler <mt-ml at gmx dot de>

_pkgname=xdg-app
pkgname=$_pkgname-git
pkgver=0.4.8.r1.gbd174d9
pkgrel=1
pkgdesc='An application deployment framework for desktop apps'
arch=('i686' 'x86_64')
url='http://www.freedesktop.org/software/xdg-app'
license=('GPL2')
depends=('glib2' 'json-glib' 'ostree' 'libseccomp' 'fuse' 'libxau')
makedepends=('git' 'docbook-xsl')
_libglnxrev='d59a63e'
install='xdg-app.install'
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("${_pkgname}::git+https://github.com/alexlarsson/xdg-app.git")
sha256sums=("SKIP")

pkgver() {
    cd "$_pkgname"
    # cutting off 'foo-' prefix that presents in the git tag
    git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "$srcdir/${_pkgname}"
    ./autogen.sh
    ./configure --prefix=/usr --libexecdir=/usr/lib --sysconfdir=/etc --enable-libxdgapp
    make
}

check() {
    cd "$srcdir/${_pkgname}"
}

package() {
    cd "$srcdir/${_pkgname}"
    make DESTDIR="$pkgdir" install

    # remove setuid flag, instead we set CAP_SYSADMIN and CAP_MKNOD in the install script
    chmod -s "$pkgdir/usr/bin/xdg-app-helper"
}
